apply plugin: 'com.moowork.node'

node {
    version = "$node_version"
    npmVersion = "$npm_version"
    download = true
    nodeModulesDir = file(buildDir)
}

// Workaround the problem with Node downloading:
// Kotlin plugin uses Gradle-metadata feature which is not compatible with node plugin download implementation
repositories.whenObjectAdded {
    if (it instanceof IvyArtifactRepository) {
        metadataSources {
            artifact()
        }
    }
}

// Configures testing for JS modules
task prepareNodePackage(type: Copy) {
    from("npm") {
        include 'package.json'
        expand(project.properties + [kotlinDependency: ""])
    }
    from("npm") {
        exclude 'package.json'
    }
    into "$node.nodeModulesDir"
}

npmInstall.dependsOn prepareNodePackage

// Combine all required compilation results in the node_modules folder
// - main compilation result (compileKotlinJs)
// - all runtime dependencies of test sourceset
task jsTestDependencies(type: Sync, group: 'build' , dependsOn: [compileKotlinJs]) {
    from compileKotlinJs.destinationDir
    into "$node.nodeModulesDir/node_modules"

    from(files {
        configurations.jsTestRuntimeClasspath.collect { File file ->
            file.name.endsWith(".jar") ?
                    zipTree(file.absolutePath).matching {
                        include '*.js'
                        include '*.js.map'
                    } :
                    files()
        }
    }.builtBy(configurations.jsTestRuntimeClasspath))
}

npmInstall.dependsOn jsTestDependencies
