// Configures publishing of Maven artifacts to Bintray

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: project.rootProject.file('gradle/pom.gradle')

// ------------- tasks
task stubSources(type: Jar) {
    classifier = 'sources'
}

task stubJavadoc(type: Jar) {
    classifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url = "https://${project.bintrayOrg}.bintray.com/${project.bintrayRepository}"
        }
    }

    publications.all { publication ->
        pom.withXml(configureMavenCentralMetadata)
        if (publication.name == 'kotlinMultiplatform') {
            publication.artifactId = "kotlinx-fs-$project.name"
        } else {
            publication.artifactId = "kotlinx-fs-$project.name-$publication.name"
        }
    }

    kotlin.targets.all { target ->
        def targetPublication = publications.findByName(target.name)
        if (targetPublication != null) {
            targetPublication.artifact stubJavadoc
        }
    }

    kotlin.targets {
        configure([metadata, jvm, js]) {
            def publication = publishing.publications[targetName]
            publication.gradleModuleMetadataFile = null
            tasks.matching { it.name == "generateMetadataFileFor${name.capitalize()}Publication" }.all {
                onlyIf { false }
            }
        }
    }
}

// Load unversioned `local.properties` file
def localProperties = new Properties()
localProperties.load(new FileInputStream("$project.rootDir/local.properties"))
localProperties.each { prop -> project.ext.set(prop.key, prop.value) }

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publish = true
    override = true // for multi-platform Kotlin/Native publishing

    pkg {
        userOrg = project.bintrayOrg
        repo = project.bintrayRepository
        name = project.bintrayPackage
        licenses = ['Apache-2.0']
        vcsUrl = vcs
        version {
            name = project.version
            vcsTag = project.version
            released = new Date()
        }
    }
}

// TODO :kludge this is required for K/N publishing
bintrayUpload.dependsOn publishToMavenLocal

// This is for easier debugging of bintray uploading problems
bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll {
        !it.name.contains('-test') && it.name != 'kotlinMultiplatform'
    }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it
    }
}
